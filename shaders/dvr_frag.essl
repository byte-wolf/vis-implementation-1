#ifndef GL_ES
#version 330 es
#endif

precision highp int;
precision highp float;
precision highp sampler3D;

in vec3 vWorldPosition;

uniform vec3 uCameraPosition;
uniform sampler3D uVolumeTexture;
uniform vec3 uVolumeSize;
uniform vec3 uBackgroundColor;
uniform vec3 uForegroundColor;

uniform vec3 uCuttingPlanePosition; // point
uniform vec3 uCuttingPlaneRotation; // normal

// --- Transfer Function / Isosurface Uniforms ---
const int MAX_ISO_POINTS = 4; // Define a maximum
uniform int uNumIsoPoints;      // Actual number of active points
uniform float uIsoValues[MAX_ISO_POINTS];
uniform float uIsoOpacities[MAX_ISO_POINTS];
uniform vec3 uIsoColors[MAX_ISO_POINTS];
uniform float uIsoRange; // A small range around the iso-value to "catch" densities


out vec4 fragColor;

// --- Transfer Function Logic ---
vec4 applyTransferFunction(float density) {
    vec4 accumulatedColor = vec4(0.0); // Start with transparent black

    for (int idx = 0; idx < uNumIsoPoints; ++idx) {
        if (idx >= MAX_ISO_POINTS) break; // Should not happen if uNumIsoPoints is correct

        float isoVal = uIsoValues[idx];
        float pointOpacity = uIsoOpacities[idx];
        vec3 pointColor = uIsoColors[idx];

        // Calculate how close the density is to the current iso-value
        // This determines the "strength" of this isosurface's contribution
        // A simple triangular or Gaussian falloff can be used.
        // 'uIsoRange' defines the "thickness" or influence of the isosurface.
        float dist = abs(density - isoVal);
        float intensity = 0.0;

        if (dist < uIsoRange) {
            intensity = 1.0 - (dist / uIsoRange); // Linear falloff
            // intensity = exp(-(dist * dist) / (2.0 * uIsoRange * uIsoRange)); // Gaussian-like
        }

        //Im applyTransferFunction, zum Testen für den ersten Iso-Punkt:
        /* if (idx == 0 && intensity > 0.0) {
            return vec4(pointColor, intensity); // Zeige Rot mit Intensität des ersten Punkts
            return vec4(1, 0, 0, intensity); // Zeige Rot mit Intensität des ersten Punkts
        }
        if (idx == 1 && intensity > 0.0) {
            return vec4(uIsoColors[idx], intensity); // Zeige Rot mit Intensität des ersten Punkts
            return vec4(0, 1, 0, intensity); // Zeige Grün mit Intensität des zweiten Punkts
        } */

        if (intensity > 0.0) {
            vec4 currentColor = vec4(pointColor, pointOpacity * intensity);
            // Alpha blending (front-to-back for ray marching)
            accumulatedColor.rgb += (1.0 - accumulatedColor.a) * currentColor.a * currentColor.rgb;
            accumulatedColor.a += (1.0 - accumulatedColor.a) * currentColor.a;

            // Optimization: if fully opaque, can stop early for this ray segment
            if (accumulatedColor.a >= 0.99) {
                break;
            }
        }
    }
    return accumulatedColor;
}

// https://tavianator.com/2011/ray_box.html
bool intersectBox(vec3 rayOrigin, vec3 rayDirection, out float tMin, out float tMax) {
    vec3 boxMin = -uVolumeSize * 0.5;
    vec3 boxMax = uVolumeSize * 0.5;

    vec3 invDir = 1.0 / rayDirection;

    vec3 tMinPlane = (boxMin - rayOrigin) * invDir;
    vec3 tMaxPlane = (boxMax - rayOrigin) * invDir;

    vec3 t1 = min(tMinPlane, tMaxPlane);
    vec3 t2 = max(tMinPlane, tMaxPlane);

    tMin = max(max(t1.x, t1.y), t1.z);
    tMax = min(min(t2.x, t2.y), t2.z);

    return tMin < tMax && tMax > 0.0;
}

void main() {
    vec3 rayOrigin = uCameraPosition;
    vec3 rayDirection = normalize(vWorldPosition - rayOrigin);

    float tMin, tMax;
    bool hit = intersectBox(rayOrigin, rayDirection, tMin, tMax);

    if (!hit) {
        discard;
    }

    tMin = max(tMin, 0.0);

    vec3 normalizedRayDirection = abs(rayDirection / uVolumeSize);
    float maxNormalizedComponent = max(normalizedRayDirection.x, max(normalizedRayDirection.y, normalizedRayDirection.z));

    float assumedTextureResolution = 512.0; // Oder 256.0. Ein Kompromiss.
    float dt;
    if (maxNormalizedComponent > 0.00001) { // Vermeide Division durch Null
         dt = (1.0 / assumedTextureResolution) / maxNormalizedComponent;
    } else {
         dt = min(uVolumeSize.x, min(uVolumeSize.y, uVolumeSize.z)) / assumedTextureResolution;
    }

    float minAllowedStep = length(uVolumeSize) / 1024.0; // Nicht mehr als ~1000 Schritte durch die Diagonale
    float maxAllowedStep = length(uVolumeSize) / 64.0;   // Nicht weniger als ~60 Schritte durch die Diagonale
    dt = clamp(dt, minAllowedStep, maxAllowedStep);

    //float dt = 0.5;
    vec4 accumulatedColor = vec4(0.0, 0.0, 0.0, 0.0); 

    for (float t = tMin; t < tMax; t += dt) {
        vec3 currentPos = rayOrigin + rayDirection * t;

        float d = dot(currentPos - uCuttingPlanePosition, uCuttingPlaneRotation);

        if (d == 0.0) {
            discard;
            // maxIntensity = 1.0; // Skip points on the plane
            // break;
        } else if (d < 0.0) {
            continue; // Skip points behind the plane
        }

        vec3 textureCoord = (currentPos / uVolumeSize) + 0.5;

        if (all(greaterThanEqual(textureCoord, vec3(0.0))) && all(lessThanEqual(textureCoord, vec3(1.0)))) {
            float density = texture(uVolumeTexture, textureCoord).r;

            vec4 sampleColor = applyTransferFunction(density);

            // Front-to-back alpha compositing
            if (sampleColor.a > 0.0) { // Only composite if the sample contributes
                accumulatedColor.rgb += (1.0 - accumulatedColor.a) * sampleColor.a * sampleColor.rgb;
                accumulatedColor.a += (1.0 - accumulatedColor.a) * sampleColor.a;
            }

            // Early ray termination if fully opaque
            if (accumulatedColor.a >= 1.0) {
                break;
            }
        }
    }

    if (accumulatedColor.a <= 0.01) { // If mostly transparent
        discard;
    }

    vec3 finalRgb = mix(uBackgroundColor, accumulatedColor.rgb, accumulatedColor.a);
    fragColor = vec4(finalRgb, accumulatedColor.a);

    /* if (maxIntensity <= 0.1) {
        discard;
    } */

    //fragColor = vec4(mix(uBackgroundColor, uForegroundColor, maxIntensity), 1.0);
}