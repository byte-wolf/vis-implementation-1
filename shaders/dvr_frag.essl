#ifndef GL_ES
#version 330 es
#endif

precision highp int;
precision highp float;
precision highp sampler3D;

// --- Vertex Attributes ---
in vec3 vTransformedPosition;
in vec3 vTransformedCameraPosition;
in vec3 vRayDirection;
in vec3 vCuttingPlanePositionTransformed;

// --- Volume Texture Uniforms ---
uniform vec3 uVolumeSize;
uniform sampler3D uVolumeTexture;

// --- Static background and foreground color uniforms ---
uniform vec3 uBackgroundColor;
uniform vec3 uForegroundColor;

// --- Cutting Plane Uniforms ---
uniform int uCuttingPlaneEnabled;
uniform vec3 uCuttingPlaneRotation;

// --- Transfer Function / Isosurface Uniforms ---
const int MAX_ISO_POINTS = 4;
uniform int uNumIsoPoints;
uniform float uIsoValues[MAX_ISO_POINTS];
uniform float uIsoOpacities[MAX_ISO_POINTS];
uniform vec3 uIsoColors[MAX_ISO_POINTS];
uniform float uIsoRange;
uniform int uIsoFalloffMode; // 0: Linear falloff, 1: Binary

// --- Render Mode Uniform ---
// 0: Maximum Intensity Projection
// 1: Maximum Intensity Projection (TF)
// 2: Accumulative
// 3: Accumulative (Alpha Blending)
// 4: First-Hit Positions (TF)
// 5: First-Hit Normals (TF)
// 6: First-Hit Solid Color (TF)
// 7: Phong Shaded
uniform int uRenderMode;

out vec4 fragColor;

float calculateSampleDistance(vec3 rayDirection) {
    float epsilon = 0.00001;
    vec3 absoluteScaledRayDirection = (abs(vRayDirection)) * uVolumeSize;
    float minComponent = min(absoluteScaledRayDirection.x, min(absoluteScaledRayDirection.y, absoluteScaledRayDirection.z));

    if(minComponent > epsilon) {
        return 1.0 / minComponent;
    } else {
        return 0.001;
    }
}

// https://tavianator.com/2011/ray_box.html
bool intersectBox(vec3 rayOrigin, vec3 rayDirection, out float tMin, out float tMax) {
    vec3 boxMin = vec3(0.0);
    vec3 boxMax = vec3(1.0);

    vec3 invDir = 1.0 / rayDirection;

    vec3 tMinPlane = (boxMin - rayOrigin) * invDir;
    vec3 tMaxPlane = (boxMax - rayOrigin) * invDir;

    vec3 t1 = min(tMinPlane, tMaxPlane);
    vec3 t2 = max(tMinPlane, tMaxPlane);

    tMin = max(max(t1.x, t1.y), t1.z);
    tMax = min(min(t2.x, t2.y), t2.z);

    return tMin <= tMax && tMax >= 0.0;
}

vec4 applyTransferFunction(float density, int falloffMode) {
    vec4 sampleProperties = vec4(0.0);

    for(int idx = 0; idx < uNumIsoPoints; ++idx) {
        if(idx >= MAX_ISO_POINTS) {
            break;
        }

        float isoVal = uIsoValues[idx];
        float pointOpacity = uIsoOpacities[idx];
        vec3 pointColor = uIsoColors[idx];

        // Calculate how close the density is to the current iso-value
        float dist = abs(density - isoVal);
        float intensity = 0.0;

        if(dist < uIsoRange) {
            if(falloffMode == 0) {
                intensity = 1.0 - (dist / uIsoRange); // Linear falloff
            } else if(falloffMode == 1) {
                intensity = 1.0; // Binary
            }
        }

        if(intensity > 0.0) {
            float currentRuleAlpha = pointOpacity * intensity;
            sampleProperties.rgb += (1.0 - sampleProperties.a) * currentRuleAlpha * pointColor;
            sampleProperties.a += (1.0 - sampleProperties.a) * currentRuleAlpha;

            if(sampleProperties.a >= 1.0) {
                break;
            }
        }
    }
    return sampleProperties;
}

bool interpolateIsoValueBetween(int idx, float density1, float density2, out float t, out vec4 sampleProperties) {
    if(idx >= MAX_ISO_POINTS || idx >= uNumIsoPoints) {
        return false;
    }

    float isoValue = uIsoValues[idx];
    float pointOpacity = uIsoOpacities[idx];
    vec3 pointColor = uIsoColors[idx];

    // Check for transition in either direction
    bool hitCondition = (density1 <= isoValue && density2 >= isoValue) || (density1 >= isoValue && density2 <= isoValue);

    if(hitCondition) {
        t = (isoValue - density1) / (density2 - density1);
        t = clamp(t, 0.0, 1.0);
        sampleProperties = vec4(pointColor, pointOpacity);
        return true;
    }

    return false;
}

float sampleVolume(vec3 textureCoordinates) {
    if(textureCoordinates.x < 0.0 || textureCoordinates.x > 1.0 ||
        textureCoordinates.y < 0.0 || textureCoordinates.y > 1.0 ||
        textureCoordinates.z < 0.0 || textureCoordinates.z > 1.0) {
        return 0.0;
    }

    return texture(uVolumeTexture, textureCoordinates).r;
}

vec3 calculateNormal(vec3 hitPos) {
    float epsilon = 0.0025;
    vec3 grad = vec3(0.0);
    grad.x = sampleVolume(hitPos + vec3(epsilon, 0.0, 0.0)) - sampleVolume(hitPos - vec3(epsilon, 0.0, 0.0));
    grad.y = sampleVolume(hitPos + vec3(0.0, epsilon, 0.0)) - sampleVolume(hitPos - vec3(0.0, epsilon, 0.0));
    grad.z = sampleVolume(hitPos + vec3(0.0, 0.0, epsilon)) - sampleVolume(hitPos - vec3(0.0, 0.0, epsilon));

    return normalize(grad);
}

// Simple pseudo-random function for jittering
float random(vec2 co) {
    // Multiply by a large number and take the fractional part
    return fract(sin(dot(co.xy, vec2(12.9898, 78.233))) * 43758.5453);
}

void maximumIntensityProjectionRender(vec3 rayOrigin, vec3 rayDirection, float tMin, float tMax, float dt, bool useTransferFunction) {
    float maxIntensity = 0.0;

    for(float t = tMin; t < tMax; t += dt) {
        vec3 textureCoord = rayOrigin + rayDirection * t;

        // Apply cutting plane only if enabled
        if(uCuttingPlaneEnabled == 1) {
            float d = dot(textureCoord - vCuttingPlanePositionTransformed, uCuttingPlaneRotation);

            if(d < 0.0) {
                continue; // Skip points behind the plane
            }
        }

        if(all(greaterThanEqual(textureCoord, vec3(0.0))) && all(lessThanEqual(textureCoord, vec3(1.0)))) {
            float density = texture(uVolumeTexture, textureCoord).r;
            maxIntensity = max(maxIntensity, density);

            // Early ray termination
            if(maxIntensity >= 1.0) {
                break;
            }
        }
    }

    if(maxIntensity <= 0.1) {
        discard;
    }

    if(useTransferFunction == true) {
        fragColor = applyTransferFunction(maxIntensity, uIsoFalloffMode);
    } else {
        //fragColor = vec4(mix(uBackgroundColor, uForegroundColor, maxIntensity) + vec3(0.2, 0.0, 0.0), 1.0);
        fragColor = vec4(uForegroundColor, maxIntensity);
    }
}

// https://developer.nvidia.com/gpugems/gpugems/part-vi-beyond-triangles/chapter-39-volume-rendering-techniques
void transferFunctionRender(vec3 rayOrigin, vec3 rayDirection, float tMin, float tMax, float dt) {
    vec4 accumulatedColor = vec4(0.0, 0.0, 0.0, 0.0);
    float stepRatio = 0.5;

    for(float t = tMin; t < tMax; t += dt) {
        vec3 currentPos = rayOrigin + rayDirection * t;

        // Apply cutting plane
        if(uCuttingPlaneEnabled == 1) {
            float d = dot(currentPos - vCuttingPlanePositionTransformed, uCuttingPlaneRotation);

            if(d < 0.0) {
                continue; // Skip points behind the plane
            }
        }

        vec3 textureCoord = currentPos;

        if(all(greaterThanEqual(textureCoord, vec3(0.0))) && all(lessThanEqual(textureCoord, vec3(1.0)))) {
            float density = texture(uVolumeTexture, textureCoord).r;

            vec4 sampleProperties = applyTransferFunction(density, uIsoFalloffMode); // Returns {premultiplied_col_A0.rgb, alpha_A0}

            if(sampleProperties.a > 0.0) {
                float alpha_corrected = 1.0 - pow(1.0 - sampleProperties.a, stepRatio);
                accumulatedColor.rgb += (1.0 - accumulatedColor.a) * sampleProperties.rgb * (alpha_corrected / sampleProperties.a);
                accumulatedColor.a += (1.0 - accumulatedColor.a) * alpha_corrected;
            }

            // Early ray termination
            if(accumulatedColor.a >= 1.0) {
                break;
            }
        }
    }

    if(accumulatedColor.a <= 0.01) {
        discard;
    }

    fragColor = accumulatedColor;
}

void transferFunctionRenderOld(vec3 rayOrigin, vec3 rayDirection, float tMin, float tMax, float dt) {
    vec4 accumulatedColor = vec4(uBackgroundColor, 0.0);

    for(float t = tMin; t < tMax; t += dt) {
        vec3 textureCoord = rayOrigin + rayDirection * t;

        // Apply cutting plane
        if(uCuttingPlaneEnabled == 1) {
            float d = dot(textureCoord - vCuttingPlanePositionTransformed, uCuttingPlaneRotation);

            if(d < 0.0) {
                continue; // Skip points behind the plane
            }
        }

        if(all(greaterThanEqual(textureCoord, vec3(0.0))) && all(lessThanEqual(textureCoord, vec3(1.0)))) {
            float density = texture(uVolumeTexture, textureCoord).r;

            vec4 sampleColor = applyTransferFunction(density, uIsoFalloffMode);

            if(sampleColor.a > 0.0) {
                accumulatedColor.rgb += (1.0 - accumulatedColor.a) * sampleColor.a * sampleColor.rgb;
                accumulatedColor.a += (1.0 - accumulatedColor.a) * sampleColor.a;
            }

            // Early ray termination
            if(accumulatedColor.a >= 1.0) {
                break;
            }
        }
    }

    if(accumulatedColor.a <= 0.01) {
        discard;
    }

    fragColor = accumulatedColor; // Output opaque color after blending with background
}

void firstHitRender(vec3 rayOrigin, vec3 rayDirection, float tMin, float tMax, float dt, int renderType) {
    vec3 hitPosition = vec3(0.0);
    vec4 hitColor = vec4(0.0);
    bool foundHit = false;

    for(float t = tMin; t < tMax; t += dt) {
        vec3 currentCoordinates = rayOrigin + rayDirection * t;
        vec3 nextCoordinates = rayOrigin + rayDirection * (t + dt);

        // Apply cutting plane
        if(uCuttingPlaneEnabled == 1) {
            float d = dot(currentCoordinates - vCuttingPlanePositionTransformed, uCuttingPlaneRotation);

            if(d < 0.0) {
                continue; // Skip points behind the plane
            }
        }

        if(all(greaterThanEqual(currentCoordinates, vec3(0.0))) && all(lessThanEqual(nextCoordinates, vec3(1.0)))) {
            float currentDensity = texture(uVolumeTexture, currentCoordinates).r;
            float nextDensity = texture(uVolumeTexture, nextCoordinates).r;

            float t;
            vec4 sampleProperties;

            bool hit = false;
            for(int idx = 0; idx < uNumIsoPoints; ++idx) {
                if(idx >= MAX_ISO_POINTS) {
                    break;
                }

                if(interpolateIsoValueBetween(idx, currentDensity, nextDensity, t, sampleProperties)) {
                    hit = true;
                    break;
                }
            }

            if(hit) {
                hitPosition = mix(currentCoordinates, nextCoordinates, t);
                hitColor = sampleProperties;
                foundHit = true;
                break; // Stop at the first hit
            }
        }
    }

    if(!foundHit) {
        discard;
    }

    if(renderType == 4) {
        // First-Hit Positions (TF)
        fragColor = vec4(hitPosition, 1.0);
    } else if(renderType == 5) {
        // First-Hit Normals (TF)
        vec3 surfaceNormal = calculateNormal(hitPosition);
        fragColor = vec4(surfaceNormal * 0.5 + 0.5, 1.0);
    } else if(renderType == 6) {
        // First-Hit Solid Color (TF)
        fragColor = vec4(hitColor.rgb, 1.0);
    }
}

vec3 applySurfaceLighting(vec3 hitPosition, vec3 surfaceColor) {
    vec3 surfaceNormal = calculateNormal(hitPosition);
    vec3 lightDirection = normalize(vec3(0.0, 2.0, 1.0));

    vec3 ambientLightColor = vec3(0.8);
    vec3 ambient = 0.7 * surfaceColor * ambientLightColor;

    vec3 directLightColor = vec3(1.0);
    float NdotL = max(dot(surfaceNormal, lightDirection), 0.0);
    vec3 diffuse = 0.5 * NdotL * surfaceColor * directLightColor;

    vec3 viewDirection = normalize(vTransformedCameraPosition - hitPosition);
    vec3 reflectionDirection = reflect(lightDirection, surfaceNormal);
    float RdotV = max(dot(reflectionDirection, viewDirection), 0.0);
    float shininess = 3.0;
    float specularIntensity = 0.3;
    float spec = pow(RdotV, shininess);
    vec3 specular = specularIntensity * spec * surfaceColor * directLightColor;

    vec3 finalColor = ambient + diffuse + specular;

    return clamp(finalColor, 0.0, 1.0);
}

void phongShadedRender(vec3 rayOrigin, vec3 rayDirection, float tMin, float tMax, float dt) {
    vec3 hitPosition = vec3(0.0);
    vec4 hitColor = vec4(0.0);
    bool foundHit = false;

    vec4 accumulatedColor = vec4(uBackgroundColor, 0.0);

    for(float t = tMin; t < tMax; t += dt) {
        vec3 currentCoordinates = rayOrigin + rayDirection * t;
        vec3 nextCoordinates = rayOrigin + rayDirection * (t + dt);

        //fragColor = vec4(currentCoordinates, 1.0);
        //return;

        // Apply cutting plane
        if(uCuttingPlaneEnabled == 1) {
            float d = dot(currentCoordinates - vCuttingPlanePositionTransformed, uCuttingPlaneRotation);

            if(d < 0.0) {
                continue; // Skip points behind the plane
            }
        }

        if(all(greaterThanEqual(currentCoordinates, vec3(0.0))) && all(lessThanEqual(nextCoordinates, vec3(1.0)))) {
            float currentDensity = texture(uVolumeTexture, currentCoordinates).r;
            float nextDensity = texture(uVolumeTexture, nextCoordinates).r;

            float t;
            vec4 sampleProperties;

            bool hit = false;
            for(int idx = 0; idx < uNumIsoPoints; idx++) {
                if(interpolateIsoValueBetween(idx, currentDensity, nextDensity, t, sampleProperties)) {
                    hitPosition = mix(currentCoordinates, nextCoordinates, t);
                    hitColor = sampleProperties;
                    foundHit = true;
                    hit = true;
                    break;
                }
            }

            if(hit) {
                vec3 litColor = applySurfaceLighting(hitPosition, hitColor.rgb);
                float remainingAlpha = (1.0 - accumulatedColor.a);
                accumulatedColor.rgb += remainingAlpha * hitColor.a * litColor;
                accumulatedColor.a += remainingAlpha * hitColor.a;
            }

            // Early ray termination
            if(accumulatedColor.a >= 1.0) {
                break;
            }
        }
    }

    if(accumulatedColor.a <= 0.1) {
        discard;
    }

    fragColor = accumulatedColor;

    /* vec3 finalColor = applySurfaceLighting(hitPosition, hitColor.rgb);

    fragColor = vec4(finalColor, hitColor.a); */

}

void main() {
    vec3 normalizedRayDirection = normalize(vRayDirection);

    float tMin, tMax;
    bool hit = intersectBox(vTransformedCameraPosition, normalizedRayDirection, tMin, tMax);

    if(!hit) {
        fragColor = vec4(1.0);
        return;
    }

    tMin = max(tMin, 0.0);

    float dt = 0.002;
    //float dt = calculateSampleDistance(normalizedRayDirection);
    //tMin += random(vTransformedPosition.xy) * dt / 2.0;

    if(uRenderMode == 0) {
        maximumIntensityProjectionRender(vTransformedCameraPosition, normalizedRayDirection, tMin, tMax, dt, false);
    } else if(uRenderMode == 1) {
        maximumIntensityProjectionRender(vTransformedCameraPosition, normalizedRayDirection, tMin, tMax, dt, true);
    } else if(uRenderMode == 2) {
        transferFunctionRender(vTransformedCameraPosition, normalizedRayDirection, tMin, tMax, dt);
    } else if(uRenderMode == 3) {
        transferFunctionRenderOld(vTransformedCameraPosition, normalizedRayDirection, tMin, tMax, dt);
    } else if(uRenderMode >= 4 && uRenderMode <= 6) {
        firstHitRender(vTransformedCameraPosition, normalizedRayDirection, tMin, tMax, dt, uRenderMode);
    } else if(uRenderMode == 7) {
        phongShadedRender(vTransformedCameraPosition, normalizedRayDirection, tMin, tMax, dt);
    }
}